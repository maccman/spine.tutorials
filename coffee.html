<!DOCTYPE html>
<html>
<head>
<meta charset=utf-8>
<title>Spine Tutorials</title>
<link rel="stylesheet" href="site/site.css" type="text/css" charset="utf-8">
<link rel="stylesheet" href="site/highlight.css" type="text/css" charset="utf-8">
<script src="site/jquery.js" type="text/javascript" charset="utf-8"></script>      
<script src="site/highlight.js" type="text/javascript" charset="utf-8"></script>
<script type="text/javascript" charset="utf-8">
  hljs.initHighlightingOnLoad();
</script>
</head>
<body>
<div id="container"><div class="back"><a href="index.html">&laquo; Back to all tutorials</a></div>


<h1>Using Spine and CoffeeScript</h1>

<p><a href="http://jashkenas.github.com/coffee-script">CoffeeScript</a> is a "little language that compiles into JavaScript". Essentially, its aim is to improve JavaScript syntax, meaning less typing for you, and often less errors.</p>

<p>CoffeeScript files compile down to pure JavaScript, and there's no interpretation at run time. This means you can use any existing JavaScript library seamlessly, and vice-versa. In other words, you can write your Spine applications in CoffeeScript, giving you a much sweeter syntax and saving time.</p>

<p>You can use Spine &amp; CoffeeScript as-is, without any API changes. Use the same function names and properties, just use CoffeeScript's syntax for defining function and object literals.</p>

<p>I'm not going to elaborate much on CoffeeScript's syntax here, see the <a href="http://jashkenas.github.com/coffee-script">official documentation</a> for that. What I am going to do though, is show you an example of a few Spine models and controllers, namely convert <a href="http://github.com/maccman/spine.todos">Spine.Todos</a> into CoffeeScript.</p>

<p>Let's take the <code>Tasks</code> controller:</p>

<pre><code>window.Tasks = Spine.Controller.create({
  tag: "li",

  proxied: ["render", "remove"],

  events: {
    "change   input[type=checkbox]": "toggle",
    "blur     input[type=text]":     "close"
  },

  init: function(){
    @item.bind("update",  @render);
  }
  // ...
</code></pre>

<p>First we're going to remove all <code>var</code> keywords, CoffeeScript will deal with scope for us. Then remove all semi colons, CoffeeScript inserts those later automatically. The next step is to use CoffeeScript's object literal syntax; as you can see braces are optional. CoffeeScript uses <code>@</code> as an alias for <code>this.</code>, so we can clean up a lot of code there. Finally, we're going to convert calls to <code>function()</code> to CoffeeScript's function syntax, namely <code>-&gt;</code>. Here's the finished result:</p>

<pre><code>Tasks = Spine.Controller.create
  tag: "li"

  proxied: ["render", "remove"]

  events:
   "change   input[type=checkbox]": "toggle",
   "blur     input[type=text]":     "close"

  init: -&gt;
    @item.bind("update", @render)
</code></pre>

<p>You can see the resultant syntax is much clearer, with less room for errors. You can also see the full <a href="https://github.com/maccman/spine.todos/blob/master/app/application.js">before</a> and <a href="https://github.com/maccman/spine.todos/blob/coffee/app/application.coffee">after</a> scripts for a comparison, and the CoffeeScript version of application.js is included below for convenience.</p>

<pre><code>Task = Spine.Model.setup("Task", ["name", "done"])

Task.extend(Spine.Model.Local)

Task.extend
  active: -&gt;
    @select (item) -&gt; !item.done

  done: -&gt;
    @select (item) -&gt; !!item.done

  destroyDone: -&gt;
    rec.destroy() for rec in @done()

Tasks = Spine.Controller.create
  tag: "li"

  proxied: ["render", "remove"]

  events:
   "change   input[type=checkbox]": "toggle",
   "click    .destroy":             "destroy",
   "dblclick .view":                "edit",
   "keypress input[type=text]":     "blurOnEnter",
   "blur     input[type=text]":     "close"

  elements:
    "input[type=text]": "input",
    ".item": "wrapper"

  init: -&gt;
    @item.bind("update",  @render)
    @item.bind("destroy", @remove)

  render: -&gt;
    elements = $("#taskTemplate").tmpl(@item)
    @el.html(elements)
    @refreshElements()
    this

  toggle: -&gt;
    @item.done = !@item.done
    @item.save()

  destroy: -&gt;
    @item.destroy()

  edit: -&gt;
    @wrapper.addClass("editing")
    @input.focus()

  blurOnEnter: (e) -&gt;
    if e.keyCode == 13 then e.target.blur()

  close: -&gt;
    @wrapper.removeClass("editing")
    @item.updateAttributes({name: @input.val()})

  remove: -&gt;
    @el.remove()

TaskApp = Spine.Controller.create    
  proxied: ["addOne", "addAll", "renderCount"]

  events:
    "submit form":   "create",
    "click  .clear": "clear"

  elements:
    ".items":     "items",
    ".countVal":  "count",
    ".clear":     "clear",
    "form input": "input"

  init: -&gt;
    Task.bind("create",  @addOne)
    Task.bind("refresh", @addAll)
    Task.bind("refresh change", @renderCount)
    Task.fetch()

  addOne: (task) -&gt;
    view = Tasks.init({item: task})
    @items.append(view.render().el)

  addAll: -&gt;
    Task.each(@addOne)

  create: (e) -&gt;
    e.preventDefault()
    Task.create({name: @input.val()})
    @input.val("")

  clear: -&gt;
    Task.destroyDone()

  renderCount: -&gt;
    active = Task.active().length
    @count.text(active)

    inactive = Task.done().length
    if inactive 
      @clear.show()
    else
      @clear.hide()

jQuery -&gt;
  TaskApp.init(el: $("#tasks"))
</code></pre>
</div>
</body>
</html>